/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    // Bump up shadow version to support Gradle 5.x https://github.com/johnrengelman/shadow
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'java'

dependencies {
    compile project(":gobblin-api")
    compile project(":gobblin-cluster")
    compile project(":gobblin-core")
    compile project(":gobblin-metrics-libs:gobblin-metrics")
    compile project(":gobblin-metastore")
    compile project(":gobblin-runtime")
    compile project(":gobblin-utility")
    compile project(":gobblin-modules:gobblin-kafka-1")

    compile externalDependency.kafka1Client
    compile externalDependency.jackson

    testCompile externalDependency.testng
    testCompile externalDependency.mockito
    testCompile(externalDependency.kafka1Test){
        exclude group: "com.sun.jmx", module: "jmxri"
        exclude group: "com.sun.jdmk", module: "jmxtools"
        exclude group: "javax.jms", module: "jms"
    }
    testCompile(externalDependency.kafka1ClientTest)

}

configurations {
    compile { transitive = false }
    // Remove xerces dependencies because of versioning issues. Standard JRE implementation should
    // work. See also http://stackoverflow.com/questions/11677572/dealing-with-xerces-hell-in-java-maven
    // HADOOP-5254 and MAPREDUCE-5664
    all*.exclude group: 'xml-apis'
    all*.exclude group: 'xerces'
}

test {
    workingDir rootProject.rootDir
}

ext.classification = "library"

// This line is added as a work around to fix a bug. Without this line, the build
// might fail intermittently with a 'Could not find property mavenDeployer' error.
// More details: check TOOLS-123257
tasks.remove(tasks.uploadShadow)

// create a single Jar with all dependencies
// Copied and adapted from gobblin-yarn
shadowJar {
    zip64 true
    dependencies {
        exclude dependency('org.eclipse.jetty:.*')
        exclude dependency('org.mortbay.jetty:.*')
        exclude dependency('org.projectlombok:.*')
        exclude dependency('org.codehaus.groovy:.*')
        exclude dependency('mysql:.*')
        exclude dependency('org.bouncycastle:.*')
        exclude dependency('org.testng:.*')
        exclude dependency('org.mockito:.*')
        exclude dependency('org.datanucleus:.*')
        exclude dependency('org.apache.hive:.*')
        exclude dependency('org.scala-lang:scala-library:.*')
        exclude dependency('org.apache.derby:.*')
        exclude dependency('org.slf4j:log4j-over-slf4j:.*')
    }
    mergeServiceFiles()
}